module analysis/comps

imports
	signatures/-
	analysis/stmts

rules
[[Main(sigParamsIn,sigParamsOut,params,stmt) ^ (globalscope)]] := 
	new mainscope,
	mainscope ---> globalscope,
	distinct/name D(mainscope) | error @NAMES,
	Map1[[sigParamsIn ^ (mainscope)]],
	Map1[[sigParamsOut ^ (mainscope)]],
	Map1T[[params ^ (mainscope) : ptys]],
	[[stmt ^ (mainscope)]].
	
[[Component(id,sigParamsIn,sigParamsOut,params,stmt) ^ (globalscope)]] := 
	Component{id} <- globalscope,
	Component{id} : CompFunc(ptys),  
	new compscope,
	compscope ---> globalscope,
	distinct/name D(compscope) | error @NAMES,
	Map1[[sigParamsIn ^ (compscope)]],
	Map1[[sigParamsOut ^ (compscope)]],
	Map1T[[params ^ (compscope) : ptys]],
	[[stmt ^ (compscope)]].	

[[SigParam(id, value) ^ (scope)]] := 
	Var{id} <- scope,
	Var{id} : TSignal(),
	Var{id}.origin := Sig(),
	[[value ^ (scope) : vty]],
	vty == TInteger() | error.
	
	
[[ParamDecl(type, id) ^ (scope) : ty]] := 
	Var{id} <- scope,
	[[type ^ (scope) : ty]],
	Var{id} : ty,
	Var{id}.origin := Param().
